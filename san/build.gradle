plugins {
    id("com.android.library")
    id("kotlin-android")
    id('maven-publish')
    id('kotlin-android-extensions')
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation("androidx.core:core-ktx:1.9.0")
    implementation("androidx.appcompat:appcompat:1.5.1")
    implementation("com.google.android.material:material:1.7.0")
    implementation 'androidx.startup:startup-runtime:1.1.1'
    implementation 'com.atlasv.android:ad-framework-gz:2.0.9'
    // 更新版本，千万记得看看WeakIniter的变化，禁了san-sdk的自启动，按需初始化，不适配可能会导致崩溃！
    api ("com.myadsget:san-sdk:3.12.7.7") // SAN sdk
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release
                artifact sourcesJar
                // You can then customize attributes of the publication as shown below.
                groupId 'com.atlasv.android'
                artifactId project.name + "-gz"
                version '2.0.8'
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/AtlasXV/android-libs")
                credentials {
                    username = System.getenv("GPR_USR") ?: project.findProperty("GPR_USR")
                    password = System.getenv("GPR_KEY") ?: project.findProperty("GPR_KEY")
                }
            }
        }
    }
}